buildscript {

    ext {
        kotlinVersion = '1.3.31'
        coroutinesAndroidVersion = '1.0.1'

        appCompatVersion = '1.1.0-alpha01'
        recyclerVersion = '1.0.0'
        preferenceVersion = '1.1.0-alpha02'
        constraintVersion = '2.0.0-alpha3'
        ktxVersion = '1.1.0-alpha03'

        materialVersion = '1.1.0-alpha02'

        lifecycleVersion = '2.2.0-alpha01'
        roomVersion = '2.1.0-alpha03'

        kotshiVersion = '1.0.3'
        retrofitVersion = '2.5.0'
        okhttpVersion = '3.10.0'
        daggerVersion = '2.20'
        glideVersion = '4.8.0'
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0-beta05'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.cookpad.android.licensetools:license-tools-plugin:1.2.0'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

enum VersionSuffix {
    ALPHA(0, "alpha"), BETA(1, "beta"), PRODUCT(9, "")

    private final int code
    private final String name

    VersionSuffix(int code, String name) {
        this.code = code
        this.name = name
    }

    int getCode() {
        code
    }

    String getName() {
        name
    }
}

ext {
    def appMajor = 1
    def appMinor = 0
    def appPatch = 0
    def appBuild = 1

    def suffix = VersionSuffix.BETA

    appVersionCode = appMajor * 1000000 + appMinor * 10000 + appPatch * 1000 + suffix.code * 100 + appBuild
    appVersionName = suffix == VersionSuffix.PRODUCT ?
            "${appMajor}.${appMinor}.${appPatch}" :
            "${appMajor}.${appMinor}.${appPatch}-${suffix.name} ${appBuild}"
}