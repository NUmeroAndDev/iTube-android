apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'com.cookpad.android.licensetools'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 2

def suffix = VersionSuffix.ALPHA

enum VersionSuffix {
    ALPHA(0, "alpha"), BETA(1, "beta"), PRODUCT(9, "")

    private final int code
    private final String name

    VersionSuffix(int code, String name) {
        this.code = code
        this.name = name
    }

    int getCode() {
        code
    }

    String getName() {
        name
    }
}

def appVersionCode = versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 1000 + suffix.code * 100 + versionBuild
def appVersionName = suffix == VersionSuffix.PRODUCT ?
        "${versionMajor}.${versionMinor}.${versionPatch}" :
        "${versionMajor}.${versionMinor}.${versionPatch}-${suffix.name} ${versionBuild}"

android {

    compileSdkVersion 28
    buildToolsVersion '28.0.1'

    defaultConfig {
        applicationId "com.numero.itube"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    androidExtensions {
        experimental = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation files('libs/YouTubeAndroidPlayerApi.jar')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.0.0-beta01'

    implementation 'com.google.android.material:material:1.0.0-beta01'
    implementation 'androidx.appcompat:appcompat:1.0.0-beta01'
    implementation 'androidx.recyclerview:recyclerview:1.0.0-beta01'
    implementation 'androidx.preference:preference:1.0.0-beta01'

    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha1'

    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"

    implementation "se.ansman.kotshi:api:$kotshi_version"
    kapt "se.ansman.kotshi:compiler:$kotshi_version"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation "androidx.lifecycle:lifecycle-viewmodel:2.0.0-beta01"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0-beta01"
    implementation "androidx.lifecycle:lifecycle-runtime:2.0.0-beta01"
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0-beta01"
    implementation 'androidx.room:room-runtime:2.0.0-beta01'
    implementation "androidx.room:room-rxjava2:2.0.0-beta01"
    kapt 'androidx.room:room-compiler:2.0.0-beta01'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha1'
}
